plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

group = 'io.github.hishidama.embulk'
version = '0.1.0'

sourceCompatibility = 8
targetCompatibility = 8
compileJava.options.encoding = 'UTF-8'

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.43"
    compileOnly "org.embulk:embulk-spi:0.10.49"
    compileOnly "org.embulk:embulk-core:0.10.49"
    implementation "org.embulk:embulk-deps:0.10.49"
    // https://mvnrepository.com/artifact/org.embulk/embulk-parser-csv
    implementation 'org.embulk:embulk-parser-csv:0.10.42'
    implementation "org.embulk:embulk-input-file:0.10.42"
    implementation 'org.embulk:embulk-formatter-csv:0.10.42'

    testImplementation "org.embulk:embulk-core:0.10.49"
    testImplementation 'junit:junit:4.+'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'hm-embulk-tester'
            from components.java

            pom {
                name = 'hm-embulk-tester'
                description = 'Tool to test Embulk plugin'
                url = 'https://github.com/hishidama/hm-embulk-tester'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'hishidama'
                        name = 'Hishida Masato'
                    }
                }
                scm {
                    connection = 'git@github.com:hishidama/hm-embulk-tester.git'
                    developerConnection = 'git@github.com:hishidama/hm-embulk-tester.git'
                    url = 'https://github.com/hishidama/hm-embulk-tester'
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = "${sonatypeUsername}"
                password = "${sonatypePassword}"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
